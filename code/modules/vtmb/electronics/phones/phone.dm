/// Index to a define to point at a runtime-global list at compile-time.
#define NETWORK_ID 1
/// Index to a string, for the contact title.
#define OUR_ROLE 2
/// Index to a boolean, on whether to replace role with job title (or alt-title).
#define USE_JOB_TITLE 3

/obj/effect/temp_visual/phone
	icon = 'icons/effects/fov/fov_effects.dmi'
	icon_state = "note"
	duration = 1 SECONDS

/particles/phone_ringing
	icon = 'icons/effects/fov/fov_effects.dmi'
	icon_state = list("note" = 1)
	width = 32
	height = 48
	count = 5
	spawning = 0.5
	lifespan = 2 SECONDS
	fade = 1.5 SECONDS
	gravity = list(0, 0.1)
	position = generator(GEN_SPHERE, 0, 16, NORMAL_RAND)
	spin = generator(GEN_NUM, -1, 1, NORMAL_RAND)

/proc/create_unique_phone_number(exchange = 415, postfix)
	var/max_num = (10 ** SUBSCRIBER_NUMBER_LENGTH) - 1

	//Slightly jank santization
	if(postfix)
		postfix = text2num(postfix)
		postfix = num2text(postfix, SUBSCRIBER_NUMBER_LENGTH, 10)
	// If we have a valid phone number set and someone hasnt already taken it
	if((postfix && postfix != "0000") && !("[exchange][postfix]" in GLOB.phone_numbers_list))
		return "[exchange][postfix]"

	// If we dont pass a postfix or cant use it, pick a random one
	var/subscriber_code
	for(var/i in 1 to 1000)
		subscriber_code = num2text(rand(1, max_num), SUBSCRIBER_NUMBER_LENGTH, 10)
		if(!("[exchange][subscriber_code]" in GLOB.phone_numbers_list))
			break

	return "[exchange][subscriber_code]"

/obj/item/vamp/phone
	name = "\improper phone"
	desc = "A portable device to call anyone you want."
	icon = 'code/modules/wod13/items.dmi'
	icon_state = "phone0"
	inhand_icon_state = "phone0"
	base_icon_state = "phone"
	lefthand_file = 'code/modules/wod13/lefthand.dmi'
	righthand_file = 'code/modules/wod13/righthand.dmi'
	item_flags = NOBLUDGEON
	flags_1 = HEAR_1
	w_class = WEIGHT_CLASS_SMALL
	armor = list(MELEE = 0, BULLET = 0, LASER = 0, ENERGY = 0, BOMB = 0, BIO = 0, RAD = 0, FIRE = 100, ACID = 100)
	resistance_flags = FIRE_PROOF | ACID_PROOF
	ONFLOOR_ICON_HELPER('code/modules/wod13/onfloor.dmi')

	var/obj/effect/abstract/particle_holder/particle_generator

	/// Sound effect for BEING called
	var/call_sound = 'code/modules/wod13/sounds/call.ogg'
	var/calling_sound = 'code/modules/wod13/sounds/call.ogg'
	var/hangup_sound = 'code/modules/wod13/sounds/phonestop.ogg'

	var/exchange_num = 415
	var/number
	var/list/contacts = list()
	var/blocked = FALSE
	var/list/blocked_contacts = list()
	var/closed = TRUE
	var/owner = ""
	var/datum/weakref/owner_ref = null
	/// The cellphone we are currently calling
	var/obj/item/vamp/phone/online
	var/talking = FALSE
	var/choosed_number = ""
	var/last_call = 0

	var/can_fold = TRUE
	var/silence = FALSE
	var/toggle_published_contacts = TRUE // APOC EDIT CHANGE // Why is this opt in!!!!
	var/list/published_numbers_contacts = list()
	var/list/phone_history_list = list()

	/// Phone icon states
	var/open_state = "phone2"
	var/closed_state = "phone1"
	var/folded_state = "phone0"

	/// A list of associative lists with three indeces: NETWORK_ID, OUR_ROLE and USE_JOB_TITLE. So that contact_networks is populated on init.
	var/list/contact_networks_pre_init = null
	/// A list of contact networks to be added in. Order matters, as if members overlap they will only get the first contact.
	var/list/datum/contact_network/contact_networks = null
	var/important_contact_of = null

	var/ringing = 0

/obj/item/vamp/phone/Initialize()
	. = ..()
	RegisterSignal(src, COMSIG_MOVABLE_HEAR, PROC_REF(handle_hearing))
	var/mob/living/carbon/human/owner = ishuman(loc) ? loc : !isnull(loc) && ishuman(loc.loc) ? loc.loc : null
	if(!number || number == "")
		if(!isnull(owner))
			src.owner = owner.real_name
		var/my_number = owner?.client?.prefs?.phone_postfix
		number = create_unique_phone_number(exchange_num, my_number)
		GLOB.phone_numbers_list += number
		GLOB.phones_list += src
		var/autopublish = owner?.client?.prefs?.phone_autopublish
		var/autopublish_name = owner?.client?.prefs?.phone_autopublish_name
		if(autopublish)
			GLOB.published_numbers += number
			GLOB.published_number_names += autopublish_name
		if(!isnull(owner) && owner.Myself)
			owner.Myself.phone_number = number

	if(LAZYLEN(contact_networks_pre_init))
		LAZYINITLIST(contact_networks)
		for(var/list/contact_network_info as anything in contact_networks_pre_init)
			var/list/network_contacts = contact_network_from_define(contact_network_info[NETWORK_ID])

			var/our_role = contact_network_info[OUR_ROLE]
			if(contact_network_info[USE_JOB_TITLE] && !isnull(owner) && owner?.job)
				var/datum/job/job = SSjob.GetJob(owner.job)
				var/alt_title = owner.client?.prefs?.alt_titles_preferences[job.title]
				our_role = alt_title ? alt_title : job.title

			var/datum/contact_network/contact_network = new(network_contacts, our_role = our_role)

			update_global_contacts(contact_network)
			contact_networks += contact_network
		contact_networks_pre_init = null

	if(important_contact_of && src.owner && number)
		GLOB.important_contacts[important_contact_of] = new /datum/phonecontact(src.owner, number)

/obj/item/vamp/phone/Destroy()
	GLOB.phone_numbers_list -= number
	GLOB.phones_list -= src
	UnregisterSignal(src, COMSIG_MOVABLE_HEAR)
	for (var/datum/contact_network/network as anything in contact_networks)
		remove_from_phone_lists(network)
	return ..()

/obj/item/vamp/phone/attack_hand(mob/user)
	. = ..()
	ui_interact(user)

/obj/item/vamp/phone/interact(mob/user)
	. = ..()
	if(ringing) // APOC EDIT ADD
		ui_interact(user)

/obj/item/vamp/phone/ui_interact(mob/user, datum/tgui/ui)
	. = ..()
	if(closed)
		closed = FALSE
		icon_state = open_state
	ui = SStgui.try_update_ui(user, src, ui)
	if(!ui)
		ui = new(user, src, "Telephone", "Phone")
		ui.open()

/obj/item/vamp/phone/AltClick(mob/user)
	if(can_fold && !closed && user.canUseTopic(src, BE_CLOSE, NO_DEXTERITY))
		closed = TRUE
		icon_state = folded_state
		talking = FALSE
		if(online)
			online.end_call()
			end_call()

/obj/item/vamp/phone/ui_data(mob/user)
	var/list/data = list()
	data["calling"] = FALSE
	if(last_call+100 > world.time && !talking)
		data["calling"] = TRUE

	data["online"] = online
	data["talking"] = talking
	data["my_number"] = choosed_number
	data["choosed_number"] = choosed_number
	if(online)
		data["calling_user"] = "(+1 [exchange_num]) [online.number]" // APOC EDIT CHANGE // Exchange number
		for(var/datum/phonecontact/P in contacts)
			if(P.number == online.number)
				data["calling_user"] = P.name

	return data

/obj/item/vamp/phone/ui_act(action, params)
	. = ..()
	if(.)
		return

	switch(action)
		if("hang")
			last_call = 0
			if(online)
				online.end_call()
			end_call()
			.= TRUE
		if("accept")
			if(online)
				talking = TRUE
				online.online = src
				online.talking = TRUE
				setup_particles(TRUE)
				online.setup_particles(TRUE)
				phone_history_list += new /datum/phonehistory(src, online, "I accepted the call")
				online.phone_history_list += new /datum/phonehistory(online, src, "They accepted the call")
			.= TRUE
		if("decline")
			if(online)
				phone_history_list += new /datum/phonehistory(src, online, "I declined the call")
				online.phone_history_list += new /datum/phonehistory(online, src, "They declined the call")

				online.end_call()
			end_call()

			.= TRUE
		if("call")
			choosed_number = replacetext(choosed_number, " ", "")
			for(var/obj/item/vamp/phone/PHN in GLOB.phones_list) //Loop through the Phone Global List
				if(PHN.number == choosed_number) // Verify if number wrote actually meets another PHN(Phone number) in the list
					blocked = FALSE // Not blocked YET.
					for(var/datum/phonecontact/blocked_contact in PHN.blocked_contacts) // Loop through the blocked numbers in the PHN Blocked LIST
						if(blocked_contact.number == number) // Verify if the caller has their number blocked by the PHN
							blocked = TRUE // If he is, Blocked is TRUE.
							to_chat(usr, span_notice("You have been blocked by this number."))
							break // Stop loops once it is found
					if(!blocked) // If the Caller is not blocked and the PHN is flipped and they are not talking, then the call goes through.
						if(!PHN.online && !PHN.talking)
							last_call = world.time
							online = PHN
							PHN.online = src
							setup_particles()
							online.setup_particles()
							ring_callback(usr)
							if(PHN.number == number)
								return
							phone_history_list += new /datum/phonehistory(src, online, "I called")
							PHN.phone_history_list += new /datum/phonehistory(online, src, "They called me")
						else
							to_chat(usr, span_notice("Caller is busy."))
			if(!online && !blocked) // If the phone is not flipped or the phone user has left the city and they are not blocked.
				if(choosed_number == "#111")
					call_sound = 'code/modules/wod13/sounds/call.ogg'
					to_chat(usr, span_notice("Settings are now reset to default.") )
				else if(choosed_number == "#228")
					call_sound = 'code/modules/wod13/sounds/nokia.ogg'
					to_chat(usr, span_notice("Code activated.") )
				else if(choosed_number == "#666")
					call_sound = 'sound/mobs/humanoids/human/scream/malescream_6.ogg'
					to_chat(usr, span_notice("Code activated.") )
				else if(choosed_number == "#34")
					if(ishuman(usr))
						var/mob/living/carbon/human/H = usr
						H.emote("moan")
					to_chat(usr, span_notice("Code activated.") )
				else
					to_chat(usr, span_notice("Invalid number.") )
			.= TRUE
		if("contacts")
			var/list/options = list("Add","Remove","Choose","Block", "Unblock", "My Number", "Publish Number", "Published Numbers", "Unpublish Number", "Call History", "Delete Call History")
			var/option = tgui_input_list(usr, "Select an option", "Contacts Option", options)
			var/result
			switch(option)
				if("Publish Number")
					if (!islist(GLOB.published_numbers))
						GLOB.published_numbers = list()
					if (!islist(GLOB.published_number_names))
						GLOB.published_number_names = list()

					var/name = tgui_input_text(usr, "Input name", "Publish Number", encode = FALSE)
					if(name && src.number)
						name = trim(copytext_char(sanitize(name), 1, MAX_MESSAGE_LEN))
						if(src.number in GLOB.published_numbers)
							to_chat(usr, span_notice("This number is already published."))
						else
							GLOB.published_numbers += src.number
							GLOB.published_number_names += name
							to_chat(usr, span_notice("Your number is now published.") )
							for(var/obj/item/vamp/phone/PHN in GLOB.phones_list)
							//Gather all the Phones in the game to check if they got the toggle for published contacts
								if(PHN.toggle_published_contacts == TRUE)
							//If they got it, their published number will be added to those phones
									var/datum/phonecontact/NEWC = new()
									var/p_number = src.number
									NEWC.number = "[p_number]"
									NEWC.name = "[name]"
									if(NEWC.number != PHN.number)
										//Check if it is not your own number that you are adding to contacts
										var/contact_found = FALSE
										for(var/datum/phonecontact/Contact in PHN.contacts)
											if(Contact.number == NEWC.number)
												//Check if the number is not already in your contact list
												contact_found = TRUE
												break
										if(!contact_found)
											PHN.contacts += NEWC
					else
						to_chat(usr, span_notice("You must input a name to publish your number.") )

				if("Unpublish Number")
					if(src.number in GLOB.published_numbers)
						var/list/numberlist = GLOB.published_numbers
						var/number_index = numberlist.Find(src.number)
						GLOB.published_numbers -= src.number
						GLOB.published_number_names -= GLOB.published_number_names[number_index]
						to_chat(usr, span_notice("Your number has been unpublished."))
						for(var/obj/item/vamp/phone/PHN in GLOB.phones_list)
							if(PHN.toggle_published_contacts == TRUE)
								for(var/datum/phonecontact/PC in PHN.contacts)
									if(PC.number == src.number)
										PHN.contacts -= PC
					else
						to_chat(usr, span_warning("Your number isn't published!"))

				if ("Published Numbers")
					var/list_length = min(length(GLOB.published_numbers), length(GLOB.published_number_names))
					for(var/i = 1 to list_length)
						var/number = GLOB.published_numbers[i]
						var/display_number_first = copytext(number, 1, 4)
						var/display_number_second = copytext(number, 4, 4 + SUBSCRIBER_NUMBER_LENGTH)
						var/split_number = display_number_first + " " + display_number_second
						var/name = GLOB.published_number_names[i]
						to_chat(usr, "- [name]: [split_number]")
				if("Add")
					var/new_contact = tgui_input_text(usr, "Input phone number", "Add Contact", null, 15)
					if(new_contact)
						var/datum/phonecontact/NEWC = new()
						new_contact = replacetext(new_contact, " ", "") //Removes spaces
						NEWC.number = "[new_contact]"
						contacts += NEWC
						var/new_contact_name = tgui_input_text(usr, "Input name", "Add Contact", encode = FALSE)
						if(new_contact_name)
							NEWC.name = "[new_contact_name]"
						else
							var/numbrr = length(contacts)+1
							NEWC.name = "Contact [numbrr]"
				if("Remove")
					var/list/removing = list()
					for(var/datum/phonecontact/CNT_REMOVE in contacts)
						if(CNT_REMOVE)
							removing += CNT_REMOVE.name
					if(length(removing) >= 1)
						result = tgui_input_list(usr, "Select a contact", "Contact Selection", sort_list(removing))
						if(result)
							for(var/datum/phonecontact/CNT_REMOVE in contacts)
								if(CNT_REMOVE.name == result)
									contacts -= CNT_REMOVE
				if("Choose")
					update_publish_contacts() // APOC EDIT ADD
					var/list/personal_contacts = list()
					for(var/datum/phonecontact/CNTCT in contacts)
						if(CNTCT)
							personal_contacts += CNTCT.name
					if(length(personal_contacts) >= 1)
						result = tgui_input_list(usr, "Select a contact", "Contact Selection", sort_list(personal_contacts))
						if(result)
							for(var/datum/phonecontact/CNTCT in contacts)
								if(CNTCT.name == result)
									if(CNTCT.number == "")
										to_chat(usr, span_notice("Sorry, [CNTCT.name] does not have a number.") )
									choosed_number = CNTCT.number
				if("Block")
					var/block_number = tgui_input_text(usr, "Input phone number", "Block Number")
					if(block_number)
						var/datum/phonecontact/blocked_contact = new()
						block_number = replacetext(block_number, " ", "") //Removes spaces
						blocked_contact.number = "[block_number]"
						blocked_contacts += blocked_contact
						var/block_contact_name = tgui_input_text(usr, "Input name", "Add name of the Blocked number", encode = FALSE)
						if(block_contact_name)
							blocked_contact.name = "[block_contact_name]"
						else
							var/number = length(blocked_contacts)+1
							blocked_contact.name = "Blocked [number]"
				if("Unblock")
					var/list/unblocking = list()
					for(var/datum/phonecontact/CNT_UNBLOCK in blocked_contacts)
						if(CNT_UNBLOCK)
							unblocking += CNT_UNBLOCK.name
					if(length(unblocking) >= 1)
						result = tgui_input_list(usr, "Select a blocked number", "Blocked Selection", sort_list(unblocking))
						if(result)
							for(var/datum/phonecontact/CNT_UNBLOCK in blocked_contacts)
								if(CNT_UNBLOCK.name == result)
									blocked_contacts -= CNT_UNBLOCK
				if("Call History")
					if(phone_history_list.len > 0)
						var/list/message_list = list()
						for(var/datum/phonehistory/PH in phone_history_list)
							//loop through the phone_history_list searching for a phonehistory datums and display them.
							var/display_number_first = copytext(PH.number, 1, 4)
							var/display_number_second = copytext(PH.number, 4, 4 + SUBSCRIBER_NUMBER_LENGTH)
							var/split_number = display_number_first + " " + display_number_second
							message_list += span_notice("[PH.call_type]: [PH.name], [split_number] at [PH.time]")
						to_chat(usr, boxed_message(jointext(message_list, "\n")))
					else
						to_chat(usr, "You have no call history.") //PSEUDO_M return to fix all this
				if("Delete Call History")
					if(phone_history_list.len > 0)
						to_chat(usr, "Your total amount of history saved is: [phone_history_list.len]")
						var/number_of_deletions = tgui_input_number(usr, "Input the amount that you want to delete", "Deletion Amount", max_value = length(phone_history_list))
						//Delete the call history depending on the amount inputed by the User
						if(number_of_deletions > phone_history_list.len)
						// Verify if the requested amount in bigger than the history list.
							to_chat(usr, "You cannot delete more items than the history contains.")
						else
							for(var/i = 1 to number_of_deletions)
								//It will always delete the first item of the list, so the last logs are deleted first
								var/item_to_remove = phone_history_list[1]
								phone_history_list -= item_to_remove
						to_chat(usr, "[number_of_deletions] call history entries were deleted. Remaining: [phone_history_list.len]")

					else
						to_chat(usr, "You have no call history to delete it.")
				if("My Number")
					var/number_first_part = copytext(number, 1, 4)
					var/number_second_part = copytext(number, 4, 4 + SUBSCRIBER_NUMBER_LENGTH)
					to_chat(usr, number_first_part + " " + number_second_part)
			.= TRUE
		if("settings")
			//Wrench Icon, more focused on toggles or later more complex options.
			var/list/options = list("Notifications and Sounds Toggle", "Published Numbers as Contacts Toggle")
			var/option = tgui_input_list(usr, "Select a setting", "Settings Selection", options)
			switch(option)
				if("Notifications and Sounds Toggle")
					if(!silence)
						//If it is true, it will check all the other sounds for phone and disable them
						silence = TRUE
						to_chat(usr, span_notice("Notifications and Sounds toggled off.") )
					else
						silence = FALSE
						to_chat(usr, span_notice("Notifications and Sounds toggled on.") )
				if ("Published Numbers as Contacts Toggle")
					if(!toggle_published_contacts)
						var/contacts_added_lenght = published_numbers_contacts.len
						var/list_length = min(length(GLOB.published_numbers), length(GLOB.published_number_names))
						log_admin("[contacts_added_lenght]") // APOC EDIT ADD // Runtimes without quotation marks
						log_admin("[list_length]")
						if(contacts_added_lenght < list_length)
						// checks the size difference between the GLOB published list and the phone published list
							var/ADDED_CONTACTS = 0
							to_chat(usr, span_notice("Refreshing contact list...") ) // APOC EDIT CHANGE
							for(var/i = 1 to list_length)
								var/number_v = GLOB.published_numbers[i]
								var/name_v = GLOB.published_number_names[i]
								var/datum/phonecontact/NEWC = new()
								NEWC.number = "[number_v]"
								NEWC.name = "[name_v]"
								if(NEWC.number != number)
									//Check if it is not your own number that you are adding to contacts
									var/contact_found = FALSE
									for(var/datum/phonecontact/Contact in contacts)
									//Check if the number is not already in your contact list
										if(Contact.number == NEWC.number)
											contact_found = TRUE
											break
									if(!contact_found)
										contacts += NEWC
										published_numbers_contacts += NEWC
										ADDED_CONTACTS +=1
							if(ADDED_CONTACTS > 1)
								to_chat(usr, span_notice("[ADDED_CONTACTS] have been added to your contact list.") ) // APOC EDIT CHANGE
						else if(contacts_added_lenght == list_length)
							to_chat(usr, span_notice("You have all the contacts in the published list already.") )
						toggle_published_contacts = TRUE
						to_chat(usr, span_notice("The toggle of the published numbers in contacts is active.") )
					else
						toggle_published_contacts = FALSE
						to_chat(usr, span_notice("The toggle of the published numbers in contacts is disabled.") )
			.= TRUE
		if("keypad")
			if(!silence)
				playsound(loc, 'sound/machines/terminal_select.ogg', 15, TRUE)
			switch(params["value"])
				if("C")
					choosed_number = ""
					.= TRUE
					return
				if("_")
					choosed_number += " "
					.= TRUE
					return

			choosed_number += params["value"]
			.= TRUE

	return FALSE


/obj/item/vamp/phone/proc/ring_callback(mob/user)
	if(last_call+100 <= world.time && !talking)
		last_call = 0
		if(online)
			online.end_call()
		end_call()
	if(!talking && online)
		if(online.silence == FALSE)
			playsound(src, calling_sound, 10, FALSE)
			playsound(online, online.call_sound, 25, FALSE)
		addtimer(CALLBACK(src, PROC_REF(ring_callback), online, user), 20)

/obj/item/vamp/phone/proc/end_call()
	QDEL_NULL(particle_generator)
	online = null
	talking = FALSE
	if(!silence)
		playsound(src, hangup_sound, 25, FALSE)

/obj/item/vamp/phone/proc/setup_particles(weakened = FALSE)
	if(!particle_generator)
		particle_generator = new(src, /particles/phone_ringing, PARTICLE_ATTACH_MOB)
		ringing = TRUE
	if(weakened)
		particle_generator.particles.spawning = 0.005
		particle_generator.particles.count = 1
		ringing = FALSE

/obj/item/vamp/phone/proc/handle_hearing(datum/source, list/hearing_args)
	var/message = hearing_args[HEARING_RAW_MESSAGE]
	if(online && talking)
		if(hearing_args[HEARING_SPEAKER])
			if(isliving(hearing_args[HEARING_SPEAKER]))
				var/voice_saying = "unknown voice"
				var/spchspn = SPAN_ROBOT
				switch(get_dist(src, hearing_args[HEARING_SPEAKER]))
					if(3 to INFINITY)
						return
					if(1 to 2)
						spchspn = "small"
					else
						spchspn = SPAN_ROBOT
				if(ishuman(hearing_args[HEARING_SPEAKER]))
					var/mob/living/carbon/human/hearing_human = hearing_args[HEARING_SPEAKER]
					voice_saying = "[age2agedescription(hearing_human.age)] [hearing_human.gender] voice ([hearing_human.voice_tag_num])"

					if(hearing_human.clan?.name == CLAN_LASOMBRA)
						message = scramble_lasombra_message(message,hearing_human)
						playsound(src, 'code/modules/wod13/sounds/lasombra_whisper.ogg', 5, FALSE, ignore_walls = FALSE)
					else
						playsound(online, 'code/modules/wod13/sounds/phonetalk.ogg', 50, FALSE)
					//new /obj/effect/temp_visual/phone(src.loc)
				var/obj/phonevoice/VOIC = new(online)
				VOIC.name = voice_saying
				VOIC.speech_span = spchspn
				VOIC.say("[message]")
				qdel(VOIC)

/obj/item/vamp/phone/street
	desc = "An ordinary street payphone"
	icon = 'code/modules/wod13/props.dmi'
	onflooricon = 'code/modules/wod13/props.dmi'
	icon_state = "payphone"
	base_icon_state = "payphone"
	anchored = TRUE
	number = "1447"
	can_fold = FALSE

	/// Phone icon states
	open_state = "payphone"
	closed_state = "payphone"
	folded_state = "payphone"

/obj/item/vamp/phone/clean
	desc = "The usual phone of a cleaning company used to communicate with employees"
	icon = 'code/modules/wod13/onfloor.dmi'
	icon_state = "redphone"
	base_icon_state = "redphone"
	anchored = TRUE
	number = "700 4424"
	can_fold = FALSE

	open_state = "redphone"
	closed_state = "redphone"
	folded_state = "redphone"

/obj/item/vamp/phone/emergency
	desc = "The 911 dispatch phone"
	icon = 'code/modules/wod13/onfloor.dmi'
	icon_state = "redphone"
	anchored = TRUE
	number = "911"
	can_fold = FALSE
	open_state = "redphone"
	closed_state = "redphone"
	folded_state = "redphone"
	var/obj/machinery/p25transceiver/clinic_transceiver
	var/obj/machinery/p25transceiver/police_transceiver

/obj/item/vamp/phone/emergency/Initialize()
	. = ..()
	GLOB.phone_numbers_list += number
	GLOB.phones_list += src

/obj/item/vamp/phone/clean/Initialize()
	. = ..()
	GLOB.phone_numbers_list += number
	GLOB.phones_list += src

/// Phone Types

// CAMARILLA

/obj/item/vamp/phone/prince
	exchange_num = 267
	contact_networks_pre_init = list(
		list(NETWORK_ID = MILLENIUM_TOWER_NETWORK, OUR_ROLE = "C.E.O.")
		, list(NETWORK_ID = VAMPIRE_LEADER_NETWORK, OUR_ROLE = "Millenium Group C.E.O.")
		)

/obj/item/vamp/phone/seneschal
	exchange_num = 267
	contact_networks_pre_init = list(
		list(NETWORK_ID = MILLENIUM_TOWER_NETWORK, OUR_ROLE = "Vice President")
		, list(NETWORK_ID = VAMPIRE_LEADER_NETWORK, OUR_ROLE = "Millenium Group Vice President")
		)

/obj/item/vamp/phone/sheriff
	exchange_num = 267
	contact_networks_pre_init = list(
		list(NETWORK_ID = MILLENIUM_TOWER_NETWORK, OUR_ROLE = "Head of Security")
		, list(NETWORK_ID = VAMPIRE_LEADER_NETWORK, OUR_ROLE = "Millenium Group Head of Security")
		)

/obj/item/vamp/phone/harpy
	exchange_num = 267
	contact_networks_pre_init = list(
		list(NETWORK_ID = MILLENIUM_TOWER_NETWORK, OUR_ROLE = "Public Relations")
		, list(NETWORK_ID = VAMPIRE_LEADER_NETWORK, OUR_ROLE = "Millenium Group Public Relations")
		)

/obj/item/vamp/phone/hound
	exchange_num = 267
	contact_networks_pre_init = list(
		list(NETWORK_ID = MILLENIUM_TOWER_NETWORK, OUR_ROLE = "Tower Security")
		)

/obj/item/vamp/phone/tower_employee
	exchange_num = 267
	contact_networks_pre_init = list(
		list(NETWORK_ID = MILLENIUM_TOWER_NETWORK, OUR_ROLE = "Tower Employee", USE_JOB_TITLE = TRUE)
		)

// VENTRUE

/obj/item/vamp/phone/ventrue_primo
	important_contact_of = CLAN_VENTRUE
	contact_networks_pre_init = list(
		list(NETWORK_ID = VAMPIRE_LEADER_NETWORK, OUR_ROLE = "Crown Blue Jazz Club Owner")
		)

// TOREADOR

/obj/item/vamp/phone/toreador_primo
	important_contact_of = CLAN_TOREADOR
	contact_networks_pre_init = list(
		list(NETWORK_ID = VAMPIRE_LEADER_NETWORK, OUR_ROLE = "Rosebud Night Club Owner")
		)

// NOSFERATU

/obj/item/vamp/phone/nosferatu_primo
	important_contact_of = CLAN_NOSFERATU
	contact_networks_pre_init = list(
		list(NETWORK_ID = VAMPIRE_LEADER_NETWORK, OUR_ROLE = "Utility Administrator")
		)

// MALKAVIAN

/obj/item/vamp/phone/malkavian_primo
	important_contact_of = CLAN_MALKAVIAN
	contact_networks_pre_init = list(
		list(NETWORK_ID = VAMPIRE_LEADER_NETWORK, OUR_ROLE = "Hospital Administrator")
		)

// LASOMBRA

/obj/item/vamp/phone/lasombra_primo
	important_contact_of = CLAN_LASOMBRA
	contact_networks_pre_init = list(
		list(NETWORK_ID = LASOMBRA_NETWORK, OUR_ROLE = "Church Administrator")
		, list(NETWORK_ID = VAMPIRE_LEADER_NETWORK, OUR_ROLE = "Church Administrator")
		)

/obj/item/vamp/phone/lasombra_caretaker
	contact_networks_pre_init = list(
		list(NETWORK_ID = LASOMBRA_NETWORK, OUR_ROLE = "Church Caretaker")
		)

// BANU HAQIM

/obj/item/vamp/phone/banu_primo
	important_contact_of = CLAN_BANU_HAQIM
	contact_networks_pre_init = list(
		list(NETWORK_ID = VAMPIRE_LEADER_NETWORK, OUR_ROLE = "SFPD Civilian Consultant")
		)

// TREMERE

/obj/item/vamp/phone/tremere_regent
	important_contact_of = CLAN_TREMERE
	contact_networks_pre_init = list(
		list(NETWORK_ID = TREMERE_NETWORK, OUR_ROLE = "Library Manager")
		, list(NETWORK_ID = VAMPIRE_LEADER_NETWORK, OUR_ROLE = "Library Manager")
		)

/obj/item/vamp/phone/archivist
	contact_networks_pre_init = list(
		list(NETWORK_ID = TREMERE_NETWORK, OUR_ROLE = "Library Archivist")
		)

/obj/item/vamp/phone/gargoyle
	contact_networks_pre_init = list(
		list(NETWORK_ID = TREMERE_NETWORK, OUR_ROLE = "Library Maintenance")
		)

// GIOVANNI

/obj/item/vamp/phone/giovanni_capo
	important_contact_of = CLAN_GIOVANNI
	contact_networks_pre_init = list(
		list(NETWORK_ID = GIOVANNI_NETWORK, OUR_ROLE = "Bank Manager")
		, list(NETWORK_ID = VAMPIRE_LEADER_NETWORK, OUR_ROLE = "Bank Manager")
		)

/obj/item/vamp/phone/giovanni_squadra
	contact_networks_pre_init = list(
		list(NETWORK_ID = GIOVANNI_NETWORK, OUR_ROLE = "Bank Security")
		)

/obj/item/vamp/phone/giovanni_famiglia
	contact_networks_pre_init = list(
		list(NETWORK_ID = GIOVANNI_NETWORK, OUR_ROLE = "Bank Employee")
		)

// VOIVODATE Apoc Edits

/obj/item/vamp/phone/voivode
	important_contact_of = list(CLAN_TZIMISCE, CLAN_OLD_TZIMISCE)
	contact_networks_pre_init = list(
		list(NETWORK_ID = VOIVODATE_NETWORK, OUR_ROLE = "Master of the Estate")
		, list(NETWORK_ID = VAMPIRE_LEADER_NETWORK, OUR_ROLE = "Master of the Estate")
		)

/obj/item/vamp/phone/bogatyr/captain
	contact_networks_pre_init = list(
		list(NETWORK_ID = VOIVODATE_NETWORK, OUR_ROLE = "Estate Guard Captain")
		)

/obj/item/vamp/phone/bogatyr
	contact_networks_pre_init = list(
		list(NETWORK_ID = VOIVODATE_NETWORK, OUR_ROLE = "Guard of the Estate")
		)

/obj/item/vamp/phone/zadruga
	contact_networks_pre_init = list(
		list(NETWORK_ID = VOIVODATE_NETWORK, OUR_ROLE = "Servant of the Estate")
		)

/obj/item/vamp/phone/voivodate
	contact_networks_pre_init = list(
		list(NETWORK_ID = VOIVODATE_NETWORK, OUR_ROLE = "Estate Family")
	)

// ANARCHS

/obj/item/vamp/phone/baron
	exchange_num = 180
	contact_networks_pre_init = list(
		list(NETWORK_ID = ANARCH_NETWORK, OUR_ROLE = "Club Manager")
		, list(NETWORK_ID = VAMPIRE_LEADER_NETWORK, OUR_ROLE = "Anarchy Rose Club Manager")
		)

/obj/item/vamp/phone/emissary
	exchange_num = 180
	contact_networks_pre_init = list(
		list(NETWORK_ID = ANARCH_NETWORK, OUR_ROLE = "Club Representative")
		, list(NETWORK_ID = VAMPIRE_LEADER_NETWORK, OUR_ROLE = "Anarchy Rose Club Representative")
		)

/obj/item/vamp/phone/bruiser
	contact_networks_pre_init = list(
		list(NETWORK_ID = ANARCH_NETWORK, OUR_ROLE = "Club Bouncer")
		)

/obj/item/vamp/phone/sweeper
	contact_networks_pre_init = list(
		list(NETWORK_ID = ANARCH_NETWORK, OUR_ROLE = "Club Bartender")
		)

// WAREHOUSE

/obj/item/vamp/phone/dealer
	exchange_num = 180
	contact_networks_pre_init = list(
		list(NETWORK_ID = WAREHOUSE_NETWORK, OUR_ROLE = "Warehouse Manager")
		, list(NETWORK_ID = VAMPIRE_LEADER_NETWORK, OUR_ROLE = "Warehouse Manager")
		)

/obj/item/vamp/phone/supply_tech
	contact_networks_pre_init = list(
		list(NETWORK_ID = WAREHOUSE_NETWORK, OUR_ROLE = "Supply Technician")
		)

// TRIADS

/obj/item/vamp/phone/triads_soldier
	contact_networks_pre_init = list(
		list(NETWORK_ID = TRIADS_NETWORK, OUR_ROLE = "Chinatown Associate")
		)

// ENDRON

/obj/item/vamp/phone/endron_lead
	exchange_num = 180
	contact_networks_pre_init = list(
		list(NETWORK_ID = ENDRON_NETWORK, OUR_ROLE = "Endron Branch Lead")
		)

/obj/item/vamp/phone/endron_exec
	contact_networks_pre_init = list(
		list(NETWORK_ID = ENDRON_NETWORK, OUR_ROLE = "Endron Executive")
		)

/obj/item/vamp/phone/endron_affairs
	contact_networks_pre_init = list(
		list(NETWORK_ID = ENDRON_NETWORK, OUR_ROLE = "Endron Internal Affairs Agent")
		)

/obj/item/vamp/phone/endron_sec_chief
	contact_networks_pre_init = list(
		list(NETWORK_ID = ENDRON_NETWORK, OUR_ROLE = "Endron Chief of Security")
		)

/obj/item/vamp/phone/endron_security
	contact_networks_pre_init = list(
		list(NETWORK_ID = ENDRON_NETWORK, OUR_ROLE = "Endron Security Agent")
		)

/obj/item/vamp/phone/endron_employee
	contact_networks_pre_init = list(
		list(NETWORK_ID = ENDRON_NETWORK, OUR_ROLE = "Endron Employee", USE_JOB_TITLE = TRUE)
		)

// MISC PROCS

/obj/item/vamp/phone/proc/update_global_contacts(datum/contact_network/network)
	var/datum/contact/our_contact = new(owner, number, network.our_role, WEAKREF(src))

	var/our_name = isnull(network.our_role) ? owner : "[owner] - [network.our_role]"
	for (var/their_name in network.contacts)
		if (their_name == owner)
			continue
		var/datum/contact/their_contact = network.contacts[their_name]

		var/obj/item/vamp/phone/their_phone = their_contact.phone_ref.resolve()
		if (isnull(their_phone)) // Physical phone item was destroyed.
			continue

		var/we_already_have_their_contact = FALSE
		for (var/datum/phonecontact/phone_contact as anything in contacts)
			if (phone_contact.number == their_phone.number)
				we_already_have_their_contact = TRUE
				break
		if (!we_already_have_their_contact)
			// We add their contact to our phone.
			var/their_contact_name = isnull(their_contact.role) ? their_contact.name : "[their_contact.name] - [their_contact.role]"
			contacts += new /datum/phonecontact(their_contact_name, their_contact.number)

		var/they_already_have_our_contact = FALSE
		for (var/datum/phonecontact/phone_contact as anything in their_phone.contacts)
			if (phone_contact.number == number)
				they_already_have_our_contact = TRUE
				break
		if (!they_already_have_our_contact)
			//We also add our contact to their phone.
			their_phone.contacts += new /datum/phonecontact(our_name, our_contact.number)
			if(isliving(their_phone.loc))
				to_chat(their_phone.loc, span_notice("A phone contact works again: that of [our_name]."))

	network.contacts[owner] = our_contact


/obj/item/vamp/phone/proc/remove_from_phone_lists(datum/contact_network/network)
	for (var/their_name in network.contacts)
		var/datum/contact/their_contact = network.contacts[their_name]

		var/obj/item/vamp/phone/their_phone = their_contact.phone_ref.resolve()
		if (isnull(their_phone)) // Physical phone item was destroyed.
			continue

		for (var/datum/phonecontact/phone_contact as anything in their_phone.contacts)
			if (phone_contact.number != number)
				continue
			their_phone.contacts -= phone_contact

	var/datum/contact/our_contact = network.contacts[owner]
	if (!isnull(our_contact) && our_contact.number == number)
		our_contact.number = null
		our_contact.phone_ref = null

	if (important_contact_of)
		our_contact = GLOB.important_contacts[important_contact_of]
		if (!isnull(our_contact) && our_contact.number == number)
			our_contact.number = null

/obj/item/vamp/phone/proc/update_publish_contacts() // APOC EDIT ADD START
	if(toggle_published_contacts)
		var/contacts_added_lenght = published_numbers_contacts.len
		var/list_length = min(length(GLOB.published_numbers), length(GLOB.published_number_names))
		if(contacts_added_lenght < list_length)
		// checks the size difference between the GLOB published list and the phone published list
			for(var/i = 1 to list_length)
				var/number_v = GLOB.published_numbers[i]
				var/name_v = GLOB.published_number_names[i]
				var/datum/phonecontact/NEWC = new()
				NEWC.number = "[number_v]"
				NEWC.name = "[name_v]"
				if(NEWC.number != number)
					//Check if it is not your own number that you are adding to contacts
					var/contact_found = FALSE
					for(var/datum/phonecontact/Contact in contacts)
					//Check if the number is not already in your contact list
						if(Contact.number == NEWC.number)
							contact_found = TRUE
							break
					if(!contact_found)
						contacts += NEWC
						published_numbers_contacts += NEWC // APOC EDIT ADD END

#undef NETWORK_ID
#undef OUR_ROLE
#undef USE_JOB_TITLE
