// Heavily based off the modern TG /datum/action/changling, i might have left 1 or 2 refrences in here.
/datum/action/imbued_edge
	background_icon_state = "background"
	button_icon = 'modular_zapoc/modules/imbued/icons/actions.dmi'
	button_icon_state = "second_sight"
	icon_icon = 'modular_zapoc/modules/imbued/icons/actions.dmi'
	/// Details displayed in fine print within the edge menu
	var/helptext = ""
	//var/related_virture
	var/related_creed
	var/edge_dots = 0
	/// Maximum stat before the ability is blocked.
	/// For example, `UNCONSCIOUS` prevents it from being used when in hard crit or dead,
	/// while `DEAD` allows the ability to be used on any stat values.
	var/req_stat = CONSCIOUS
	/// usable when the changel
	/// used by a few powers that toggle
	var/active = FALSE

/*
imbued code now relies on on_purchase to grant powers.
if you override it, MAKE SURE you call parent or it will not be usable
the same goes for Remove(). if you override Remove(), call parent or else your power won't be removed on respec
*/

/datum/action/imbued_edge/proc/on_purchase(mob/user, is_respec)
	Grant(user)//how powers are added rather than the checks in mob.dm

/*
/datum/action/imbued_edge/IsAvailable()
	. = ..()
	var/datum/antagonist/imbued/imbued = IS_IMBUED(owner)
	if(!imbued || (imbued.conviction < get_conviction_cost()))
		return FALSE
*/

/datum/action/imbued_edge/Trigger(trigger_flags)
	var/mob/user = owner
	if(!user || !isimbued(user))
		return
	try_edge(user)

//requires since for some reason 4 dot costs 5 and 5 dots cost 6. If the action can be used to cancel an ability or should otherwise be free it can override this
/datum/action/imbued_edge/proc/get_conviction_cost()
	if(edge_dots < 4)
		return max(edge_dots, 0)
	else
		return max(edge_dots + 1, 0)

/**
 *This handles the activation of the action and the deducation of its cost.
 *The order of the proc chain is:
 *can_use_edge(). Should this fail, the process gets aborted early.
 *edge_action(). This proc usually handles the actual effect of the action.
 *Should edge_action succeed the following will be done:
 *usage_feedback(). Produces feedback on the performed action. Don't ask me why this isn't handled in edge_action()
 *The deduction of the cost of this power.
 *Returns TRUE on a successful activation.
 */
/datum/action/imbued_edge/proc/try_edge(mob/living/user, mob/living/target)
	if(!can_use_edge(user, target))
		return FALSE
	if(!isimbued(user))
		return
	var/mob/living/carbon/human/imbued = user
	if(edge_action(user, target))
		usage_feedback(user, target)
		var/conviction_cost = get_conviction_cost()
		//Only bother with updating it it will change
		if(conviction_cost > 0)
			imbued.adjust_conviction(-conviction_cost)
		user.changeNext_move(CLICK_CD_MELEE)
		return TRUE
	return FALSE

/datum/action/imbued_edge/proc/edge_action(mob/living/user, mob/living/target)
	SHOULD_CALL_PARENT(TRUE)
	SSblackbox.record_feedback("nested tally", "edge_used", 1, list("[name]"))
	return FALSE

/datum/action/imbued_edge/proc/usage_feedback(mob/living/user, mob/living/target)
	return FALSE

// Fairly important to remember to return 1 on success >.< // Return TRUE not 1 >.<
/datum/action/imbued_edge/proc/can_use_edge(mob/living/user, mob/living/target)
	if(!isimbued(user))
		return
	var/mob/living/carbon/human/imbued = user
	if(imbued.conviction < get_conviction_cost())
		user.balloon_alert(user, "needs [get_conviction_cost()] conviction!")
		return FALSE
	if(req_stat < user.stat)
		user.balloon_alert(user, "incapacitated!")
		return FALSE
	return TRUE

/atom/movable/screen/alert/status_effect/imbued
	name = "Imbued Ability"
	icon = 'modular_zapoc/modules/imbued/icons/actions.dmi'
	icon_state = "second_sight"
